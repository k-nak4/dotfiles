# zshの補完候補が画面から溢れるときに、表示するかどうかを確認する
export LISTMAX=0

export LSCOLORS=cxgxccdxbxegedabagacad

export ZLS_COLORS="${LS_COLORS}"
export CLICOLOR="true"

# zshの補完機能を強化
autoload -Uz compinit && compinit -u

# 色をつける
autoload -Uz colors && colors

# Prompt
# 上から順に、ユーザ名、ホスト名、カレントディレクトリ、グループ名、プロンプト
# プロンプトは、スーパーユーザ時に #
# スーパーユーザ以外かつ直前のコマンドの終了コードが 0 であるなら !, 0 でないなら $
p_usr="%{${fg[green]}%}%n%{${reset_color}%}"
p_hst="%{${fg[magenta]}%}@%m%{${reset_color}%}"
p_dir="%{${fg[cyan]}%}:%/%{${reset_color}%}"
p_grp="[${GROUP}]"
p_prt="%(?!%(!.#.$) !? )"
PROMPT="${p_usr}${p_hst}${p_dir}"$'\n'"${p_grp}${p_prt}"

# if ... fi とか "..." など、複数行にわたる場合
PROMPT2="%{${fg[yellow]}%}%_> %{${reset_color}%}"

# コマンド訂正時
SPROMPT="%{${fg[yellow]}%}correct%{${reset_color}%}: %{${fg[red]}%}%R%{${reset_color}%} => %{${fg[cyan]}%}%r%{${reset_color}%} (y|n|a|e)? "

# 右に出るプロンプト。日時を表示する。
RPROMPT="[%D{%m/%d %H:%M}]"

# pyenvの状態表示
PROMPT+="$ZSH_PYTHON_PROMPT"

# Git を使っているときにリポジトリの状態を表示する。
# 非力なマシンだと表示に時間がかかるので、現状コメントアウト。
autoload -Uz is-at-least
if is-at-least 4.3.10; then
   autoload -Uz vcs_info
   zstyle ':vcs_info:git:*' check-for-changes true
   zstyle ':vcs_info:git:*' stagedstr "%F{yellow}!"
   zstyle ':vcs_info:git:*' unstagedstr "%F{red}+"
   zstyle ':vcs_info:*' formats "%F{green}%c%u[%b]%f"
   zstyle ':vcs_info:*' actionformats '[%b|%a]'
   precmd () { vcs_info }
   RPROMPT=$RPROMPT'${vcs_info_msg_0_}'
fi

# キーバインドをviライクにする
bindkey -v

# オプションシリーズ。ググったり、man を読んだりする。
setopt auto_list
setopt auto_menu
setopt auto_param_slash
setopt auto_pushd
setopt auto_remove_slash
setopt auto_resume
setopt brace_ccl
setopt complete_aliases
setopt correct
#setopt extended_glob
setopt extended_history
setopt hist_ignore_dups
setopt hist_ignore_all_dups
setopt hist_ignore_space
setopt hist_reduce_blanks
setopt hist_verify
setopt ignore_eof
setopt interactive_comments
setopt list_packed
setopt list_types
setopt magic_equal_subst
setopt mark_dirs
#setopt no_flow_control
setopt nobeep
setopt nolistbeep
setopt notify
setopt numeric_glob_sort
setopt print_eight_bit
setopt prompt_subst
setopt pushd_ignore_dups
setopt share_history
setopt zle
unsetopt glob_dots
unsetopt promptcr

# 補完候補の色付け、メッセージの設定
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' verbose yes
zstyle ':completion:*' completer _expand _complete _match _prefix _approximate _list _history
zstyle ':completion:*:messages' format "%{${fg[cyan]}%}%d%{${reset_color}%}"
zstyle ':completion:*:warnings' format "%{${fg[red]}%}no matches for: %{${fg[yellow]}%}%d%{${reset_color}%}"
zstyle ':completion:*:descriptions' format "%{${fg[yellow]}%}completing %d%{${reset_color}%}"
zstyle ':completion:*:corrections' format "%{${fg[yellow]}%}%d %{${fg[red]}%}(errors: %e){${reset_color}%}"
zstyle ':completion:*:options' description 'yes'
zstyle ':completion:*' group-name ''

# これらのファイルは補完候補にしない。でも rm では候補にする。
zstyle ':completion:*:*:(^rm):*:*files' ignored-patterns '*?.o|*?.d|*?.aux|*?~|*\#'

# 補完で大文字小文字を区別しない。
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

# 補完候補をカーソルで選ぶ。
zstyle ':completion:*:default' menu select=1

# cd 時にカレントディレクトリに候補がなかったら cdpath から選ぶ。
zstyle ':completion:*:cd:*' tag-order local-directories path-directories

# 補完時にこれらのディレクトリは除外する。
zstyle ':completion:*:cd:*' ignored-patterns '*CVS|*.svn|*.git|*lost+found'

# cd 時にカレントディレクトリを候補にしない。例えば、cd ../<TAB> とすれば分かる。
zstyle ':completion:*:cd:*' ignore-parents parent pwd

# kill 候補を色付け
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([%0-9]#)*=0=01;31'

# math function を有効化する。zsh は浮動小数点を使えるのがいい。
zmodload -i zsh/mathfunc
